#!/usr/bin/env raku

use Test;

use Pakku::Core;

my $core;

lives-ok {
  $core =
  class {
    also does Pakku::Core;

    submethod BUILD( :%!cnf ) {

      my $stage  = $*TMPDIR.add( '.pakku' ).add( 'stage' ).add( rand );

			my @recman = ( %( :name<pakku>, :url<http://recman.pakku.org>, :1priority, :active ), );

      $!log    = Pakku::Log.new: :verbose<silent>;
      $!recman = Pakku::Recman.new: :@recman;
      $!cached = $stage.add( 'cached' );
      @!repo   = $*REPO.repo-chain.grep( CompUnit::Repository::Installation );

    }
  } .new
}, 'Compose Pakku::Core';

ok  $core.satisfied( spec => Pakku::Spec.new( { any => [ "curl:ver<4>:from<native>", "curl:from<native>" ] } ) ), 'Native dependency exists' if $*KERNEL ~~ 'linux';
nok $core.satisfied( spec => Pakku::Spec.new( { :from<native>, :name<missing> } ) ), 'Native dependency missing';

ok  $core.satisfied( spec => Pakku::Spec.new( { :from<bin>, :name<raku>    } ) ), 'Binary dependency exists';
nok $core.satisfied( spec => Pakku::Spec.new( { :from<bin>, :name<missing> } ) ), 'Binary dependency missing';

ok  $core.satisfied( spec => Pakku::Spec.new( 'rakudo'          ) ), 'Raku dependency exists';
nok $core.satisfied( spec => Pakku::Spec.new( 'rakudo:ver<-42>' ) ), 'Raku dependency missing';

dies-ok  { $core.satisfy( spec => Pakku::Spec.new( { :from<native>, :name<archive> } ) ) }, 'Native dependency no satisfy';
dies-ok  { $core.satisfy( spec => Pakku::Spec.new( { :from<bin>,    :name<raku>    } ) ) }, 'Binary dependency no satisfy';


lives-ok {

my $meta = $core.satisfy( spec => Pakku::Spec.new( 'Pakku' ) );

my $prefix = $core.fetch( :$meta ); 

my $dist = $meta.to-dist( :$prefix );

}, 'Can satisfy and fetch dependency';

done-testing;
