use Test;
use Pakku::Spec;
use Pakku::Grammar::Cmd;

my @tests = (
  "add Spec" =>
    ${:add(${:spec($["Spec"])}), :cmd("add")},
  "add nodeps" =>
    ${:add(${:spec($["nodeps"])}), :cmd("add")},
  "add nodeps Spec" =>
    ${:add(${:!deps, :spec($["Spec"])}), :cmd("add")},
  "add build Spec" =>
    ${:add(${:build, :spec($["Spec"])}), :cmd("add")},
  "add test Spec" =>
    ${:add(${:spec($["Spec"]), :test}), :cmd("add")},
  "add force Spec" =>
    ${:add(${:force, :spec($["Spec"])}), :cmd("add")},
  "add nobuild Spec" =>
    ${:add(${:!build, :spec($["Spec"])}), :cmd("add")},
  "add notest Spec" =>
    ${:add(${:spec($["Spec"]), :!test}), :cmd("add")},
  "add Spec1 Spec2" =>
    ${:add(${:spec($["Spec1", "Spec2"])}), :cmd("add")},
  "add noforce Spec" =>
    ${:add(${:!force, :spec($["Spec"])}), :cmd("add")},
  "add depsforce Spec" =>
    ${:add(${:spec($["depsforce", "Spec"])}), :cmd("add")},
  "add nodeps force Spec" =>
    ${:add(${:!deps, :force, :spec($["Spec"])}), :cmd("add")},
  "v3 add force Spec" =>
    ${:add(${:force, :spec($["Spec"])}), :cmd("add"), :pakku(${:verbose("info")})},
  "v 3 add nodeps Spec" =>
    ${:add(${:!deps, :spec($["Spec"])}), :cmd("add"), :pakku(${:verbose("info")})},
  "pretty v3 add force Spec" =>
    ${:add(${:force, :spec($["Spec"])}), :cmd("add"), :pakku(${:pretty, :verbose("info")})},
  "upgrade Dist" =>
    ${:upgrade(${:spec($["Dist"])}), :cmd("upgrade")},
  "remove Spec1 Spec2" =>
    ${:cmd("remove"), :remove(${:spec($["Spec1", "Spec2"])})},
  "l" =>
    ${:cmd("list")},
  "list" =>
    ${:cmd("list")},
  "list details Spec" =>
    ${:cmd("list"), :list(${:details, :spec($["Spec"])})},
  "list Spec" =>
    ${:cmd("list"), :list(${:spec($["Spec"])})},
  "search Spec" =>
    ${:cmd("search"), :search(${:spec($["Spec"])})},
  "search details Spec" =>
    ${:cmd("search"), :search(${:details, :spec($["Spec"])})},
  "config" =>
    ${:cmd("config")},
  "download Spec" =>
    ${:cmd("download"), :download(${:spec($["Spec"])})},
  "down Spec" =>
    ${:cmd("download"), :download(${:spec($["Spec"])})},
  "add" =>
    ${:cmd("help"), :help(${:cmd("add")})},
  "help" =>
    ${:cmd("help"), :help(${:cmd("")})},
  "help add" =>
    ${:cmd("help"), :help(${:cmd("add")})},
  "a" =>
    ${:cmd("help"), :help(${:cmd("add")})},
  "no cmd" =>
    ${:cmd("help"), :help(${:cmd("")})},
  "help no cmd" =>
    ${:cmd("help"), :help(${:cmd("")})},
);


for @tests {

  my $cmd      = .key;
  my $expected = .value;

  my $parser  = Pakku::Grammar::Cmd;
  my $actions = Pakku::Grammar::CmdActions;
  my $got     = $parser.parse( $cmd, :$actions ).made;


  $got<pakku>:delete unless $got<pakku>;
  $got<list>:delete  unless $got<list>;

  $got .= deepmap( -> $_ { $_ ~~ Pakku::Spec::Raku ?? .name !! $_ } );

  is-deeply  $got, $expected, $cmd;
}

done-testing;
