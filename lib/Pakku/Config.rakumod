use X::Pakku;
use Pakku::Log;

unit role Pakku::Config;

my class Config { ... }

multi method fly ( 'config', *%config ) {

  my @arg;
  my %arg;

  my $module      = %config<module>      if %config<module>;
  my $operation   = %config<operation>   if %config<operation>;
  my $recman-name = %config<recman-name> if %config<recman-name>;
  my $log-level   = %config<log-level>   if %config<log-level>;
  my $option      = %config<option>      if %config<option>;

  @arg.push( $module    ) if $module; 
  @arg.push( $operation ) if $operation; 

  %arg<recman-name> =  $recman-name if $recman-name; 
  %arg<log-level>   =  $log-level   if $log-level; 
  %arg<option>      =  $option      if $option; 

  Config.new( config-file => self!cnf<pakku><config> ).config( |@arg, |%arg );

}

my class Pakku {

  has Bool $.pretty;
  has Bool $.async;
  has Any  $.cache;
  has Bool $.yolo;
  has Bool $.please;
  has Bool $.dont;
  has Any  $.recman;
  has Any  $.norecman;
  has Str  $.verbose;

}

my class Add {

  has Bool $.build;
  has Bool $.test;
  has Bool $.xtest;
  has Bool $.force;
  has Bool $.precompile;
  has Any  $.deps;
  has Str  $.to;
  has Str  @.exclude;

}

my class Update {

  has Bool $.clean;
  has Bool $.build;
  has Bool $.test;
  has Bool $.xtest;
  has Bool $.force;
  has Bool $.precompile;
  has Any  $.deps;
  has Str  $.in;
  has Str  @.exclude;

}

my class State {

  has Bool $.clean;
  has Bool $.updates;

}


my class Remove {

  has Str  $.from;

}

my class Download { }

my class Build    { }

my class Test {

  has Bool $.build;
  has Bool $.xtest;

}

my class List {

  has Bool $.details;
  has Str  $.repo;

}

my class Search {

  has Bool  $.details;
  has Bool  $.relaxed;
  has Int() $.count;

}

my class Recman {

  has Str   $.name;
  has Str   $.location;
  has Int() $.priority;
  has Bool  $.active;

}

my class Log {

  has Str $.prefix;
  has Any $.color;

}

my class Config {

  has Pakku    $.pakku;
  has Add      $.add;
  has Update   $.update;
  has Search   $.search;
  has Remove   $.remove;
  has Build    $.build;
  has Test     $.test;
  has List     $.list;
  has Download $.download;
  has State    $.state;
  has Recman   $.recman;
  has Log      $.log;

  has $!config-file;

  has %!default-configuration;
  has %!configuration;


  multi method config ( Str:D $module, Pair:D :@option!, Str :$recman-name, Str :$log-level ) {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;

    # smart match against pair
    @option.map( -> $option {

      unless try self."$module"().new( |$option ) ~~ $option {

        üêû "CNF: " ~ "ÔΩ¢{ to-json $option, :!pretty }ÔΩ£ invalid option";

        die X::Pakku::Cnf.new( cnf => "$module" );
      }

    } );

    my %config-key;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    given $module {

      when 'recman' {

        ü¶ã qq[REC: ÔΩ¢$recman-nameÔΩ£];

        my $index = quietly %!configuration{ $module }.first( *.<name> eq $recman-name, :k );

        if defined $index {

          %config-key := %!configuration{ $module }[ $index ];

        } else {

          %!configuration{ $module }.unshift( { name => $recman-name, :1priority, :active } ); 

          %config-key := %!configuration{ $module }[ 0 ];

        }
      }

      when 'log' {

        ü¶ã qq[LOG: ÔΩ¢$log-levelÔΩ£];

        %config-key := %!configuration{ $module }{ $log-level }

      }

      default { %config-key := %!configuration{ $module } }

    }

    @option.map( -> $option {

      my $key   = $option.key;
      my $value = $option.value; 

      %config-key{ $key } = $value;

      %config-key{ $key }:delete without $value; # remove null values

      ü¶ã qq[CNF: ÔΩ¢{ to-json $option, :!pretty }ÔΩ£];

    } );

    self!write-config;

  }

  multi method config ( Str:D $module, 'unset', :$recman-name! ) {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;

    my $recman = quietly %!configuration{ $module }.first( *.<name> eq $recman-name );

    üêû qq[REC: ÔΩ¢$recman-nameÔΩ£ does not exist!] unless $recman;

    quietly ü¶ã "CNF: " ~ "ÔΩ¢$recman-nameÔΩ£" ~ "\n" ~ to-json $recman with $recman;

    %!configuration{ $module } .= grep( not *.<name> eq $recman-name );

    self!write-config;

  }

  multi method config ( Str:D $module, 'enable', :$recman-name! ) {

    my Pair @option = :active;

    samewith $module, :@option, :$recman-name;

  }

  multi method config ( Str:D $module, 'disable', :$recman-name! ) {

    my Pair @option = :!active;

    samewith $module, :$recman-name, :@option;

  }


  multi method config ( Str:D $module, 'unset', :$log-level! ) {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;


    üêû qq[LOG: ÔΩ¢$log-levelÔΩ£ does not exist!] unless %!configuration{ $module }{ $log-level }:exists;

    my $level = %!configuration{ $module }{ $log-level }:delete;

    quietly ü¶ã "CNF: " ~ "ÔΩ¢$log-levelÔΩ£" ~ "\n" ~ to-json $level with $level;

    self!write-config;
  }

  multi method config ( Str:D $module, 'view', Str :$recman-name!, Str :@option! )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    my $recman = quietly %!configuration{ $module }.first( *.<name> eq $recman-name );

    if $recman {

      ü¶ã qq[REC: ÔΩ¢$recman-nameÔΩ£];

      @option.map( -> $option { out to-json $recman{ $option }:p } );

    } else {

      üêû qq[REC: ÔΩ¢$recman-nameÔΩ£ does not exist!];

    }

  }

  multi method config ( Str:D $module, 'view', Str :$recman-name! )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    my $recman = quietly %!configuration{ $module }.first( *.<name> eq $recman-name );

    if $recman {

      ü¶ã qq[REC: ÔΩ¢$recman-nameÔΩ£];

      my Str $json = to-json $recman;

      out $json;

    } else {

      üêû qq[REC: ÔΩ¢$recman-nameÔΩ£ does not exist!];

    }

  }

  multi method config ( Str:D $module, 'view', Str :$log-level!, Str :@option! )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    my $level = quietly %!configuration{ $module }{ $log-level };

    if $level {

      ü¶ã qq[LOG: ÔΩ¢$levelÔΩ£];

      @option.map( -> $option { out to-json $level{ $option }:p } );

    } else {

      üêû qq[LOG: ÔΩ¢$log-levelÔΩ£ does not exist!];

    }

  }

  multi method config ( Str:D $module, 'view', Str :$log-level! )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    my $level = quietly %!configuration{ $module }{ $log-level };

    if $level {

      ü¶ã qq[LOG: ÔΩ¢$log-levelÔΩ£];

      my Str $json = to-json $level;

      out $json;

    } else {

      üêû qq[LOG: ÔΩ¢$log-levelÔΩ£ does not exist!];

    }

  }

  multi method config ( Str:D $module, 'view', Str :@option! )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    sink @option.map( -> $option { out to-json %!configuration{ $module }{ $option }:p } );

  }

  multi method config ( Str:D $module, 'view'  )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;

    ü¶ã qq[CNF: ÔΩ¢$moduleÔΩ£];

    my Str $json = to-json %!configuration{ $module };

    out $json;

  }


  multi method config ( Str:D $module, 'reset' )  {

   üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    %!configuration{ $module } = %!default-configuration{ $module };

    my Str $json = to-json %!configuration{ $module };

    ü¶ã "CNF: " ~ "ÔΩ¢$moduleÔΩ£" ~ "\n" ~ $json;

    self!write-config;
    
  }

  multi method config ( Str:D $module, 'unset' )  {

   üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    my Str $json = to-json %!configuration{ $module }:delete;

    ü¶ã "CNF: " ~ "ÔΩ¢$moduleÔΩ£" ~ "\n" ~ $json;

    self!write-config;
    
  }

  multi method config ( 'reset' ) {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    self!check-config-file-exists;

    %!configuration = %!default-configuration;

    self!write-config;
    
  }

  multi method config ( 'view' )  {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];

    self!check-config-file-exists;

    my Str $json = to-json %!configuration;

    out $json;

  }

  multi method config ( 'new' ) {

    üêõ qq[CNF: ÔΩ¢$!config-fileÔΩ£];
    
    if $!config-file.e {

      üêû qq[CNF: ÔΩ¢$!config-fileÔΩ£ already exists!];

      die X::Pakku::Cnf.new: cnf => $!config-file; 

    }

    $!config-file.dirname.IO.mkdir unless $!config-file.dirname.IO.e;

    %!configuration = %!default-configuration;

    self!write-config;
    
    üßö qq[CNF: ÔΩ¢$!config-fileÔΩ£];
  }

  method !write-config ( ) {

    my Str:D $json = to-json %!configuration;

    $!config-file.spurt: $json;

    üêõ "CNF: " ~ "\n" ~ $json;
  }

  method !check-config-file-exists ( ) {

    unless $!config-file.e {

      üêû "CNF: " ~ "ÔΩ¢$!config-fileÔΩ£ does not exist! to create run: pakku config new";

      die X::Pakku::Cnf.new: cnf => $!config-file; 

    }

  }

  method configuration ( ) { %!configuration }

  submethod BUILD ( IO :$!config-file! ) {

    %!default-configuration = from-json slurp %?RESOURCES<default-config.json>;

    %!configuration = from-json slurp $!config-file if $!config-file.e;

  }

  sub from-json ( Str $json --> Hash:D ) { Rakudo::Internals::JSON.from-json: $json }

  sub to-json ( \obj, :$pretty = True  --> Str:D ) { Rakudo::Internals::JSON.to-json: obj, :$pretty, :sorted-keys; }
}
