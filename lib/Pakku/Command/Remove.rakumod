use Pakku::Log;
use Pakku::Spec;

unit role Pakku::Command::Remove;


multi method fly ( 'remove', :@spec!, Str :$from ) {

  log 'ðŸ§š', header => 'RMV', msg => "ï½¢{ @spec }ï½£";

  # Get the current raku `core` dist name
  my @forced  = CompUnit::RepositoryRegistry.repository-for-name('core').candidates('Test');

  my @repo = $from ?? self.repo-from-spec( spec => $from ) !! self!repo;


  sink @repo
    ==> map( -> $repo {

      sink @spec.map( -> $str {

        my $spec = Pakku::Spec.new: $str;
        my @dist = $repo.candidates( $spec.name, |$spec.spec );

        log 'ðŸ›', header => 'SPC', msg => "ï½¢$specï½£", comment => "{ $repo.prefix}: not added!" unless @dist;

        if any( @dist.map( *.meta ) ) ~~ any( @forced.map( *.meta ) ) {

          unless self!force {

            log 'ðŸž', header => 'RMV', msg => "ï½¢$specï½£", comment => 'use force to remove!';

            die X::Pakku::Remove.new: :$spec;
          }

        }

        sink @dist.map( -> $dist {

          $repo.uninstall: $dist;

          log 'ðŸ§š', header => 'RMV', msg => "ï½¢$distï½£";

        } ) unless self!dont;

      } );
    } );
}

